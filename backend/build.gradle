plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4' apply false
	id 'io.spring.dependency-management' version '1.1.7' apply false
	id "org.flywaydb.flyway" version "9.0.0"
}

group = 'com.micromarket'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

subprojects {
	apply plugin: 'java'

	repositories {
		mavenCentral()
	}
	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa:3.4.4'
		implementation "org.flywaydb:flyway-core:9.0.0"
		compileOnly 'org.projectlombok:lombok:1.18.30'
		annotationProcessor 'org.projectlombok:lombok:1.18.30'
		testImplementation 'org.junit.jupiter:junit-jupiter'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
		implementation "org.mapstruct:mapstruct:1.5.5.Final"
		annotationProcessor "org.mapstruct:mapstruct-processor:1.5.5.Final"
		testAnnotationProcessor "org.mapstruct:mapstruct-processor:1.5.5.Final"
	}

	tasks.withType(Test).configureEach {
		useJUnitPlatform()
	}
}

project(':common') {
	dependencies {
		implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.18.3'
		implementation 'org.apache.tomcat.embed:tomcat-embed-core:11.0.6'
	}
}

project(':core-service') {
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	dependencies {
		implementation project(':common')

		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'com.zaxxer:HikariCP:5.1.0'
		runtimeOnly 'org.postgresql:postgresql'
		annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}
}

project(':payment-service') {
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	dependencies {
		implementation project(':common')

		implementation 'org.springframework.boot:spring-boot-starter-web'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}
}
